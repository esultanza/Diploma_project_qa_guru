buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java-library'
    id 'application'
    id 'io.qameta.allure' version '2.8.1'
}

group 'cloud.autotests'
version '1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8
defaultTasks 'help'

ext {
    allureVersion = "2.13.6"
    selenideVersion = "5.16.0"
    junitVersion = "5.7.0"
}

repositories {
    maven {url = "https://dl.bintray.com/qameta/maven-unstable/"}
    mavenCentral()
}

allure {
    version = "${allureVersion}"
    autoconfigure = true
    aspectjweaver = true
    useJUnit5 {
        version = "${allureVersion}"
    }
}

dependencies {
    testImplementation (
            "org.aspectj:aspectjweaver:1.9.5",
            "com.codeborne:selenide:${selenideVersion}",
            "io.qameta.allure:allure-selenide:${allureVersion}",
            "io.rest-assured:rest-assured:4.1.2",
            "io.qameta.allure:allure-rest-assured:${allureVersion}",
            "com.fasterxml.jackson.core:jackson-annotations:2.10.3",
            "io.appium:java-client:7.3.0",
            "ch.qos.logback:logback-classic:1.2.3",
            "org.aeonbits.owner:owner:1.0.12",
            "io.qameta.allure:allure-ee-junit-platform:3.28.2",
            "org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties = System.properties
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

test {
    useJUnitPlatform()
}

task web(type: Test) {
    useJUnitPlatform {
        includeTags 'web'
    }
    systemProperty 'platform', 'web'
}

task android(type: Test) {
    useJUnitPlatform {
        includeTags 'android'
    }
    systemProperty 'platform', 'android'
}

task ios(type: Test) {
    useJUnitPlatform {
        includeTags 'ios'
    }
    systemProperty 'platform', 'ios'
}

help {
    doFirst {
        println ""
        println "************************************************************************ "
        println "***                                                                  *** "
        println "*** Welcome to                                                       *** "
        println "*** https://github.com/esultanza/Diploma_project_qa_guru             *** "
        println "***                                                                  *** "
        println "*** Requirements for mobile tests:                                   *** "
        println "*** - get browserstack account                                       *** "
        println "*** - start appium server locally                                    *** "
        println "*** - install XCode with iPhone Simulators                           *** "
        println "***                                                                  *** "
        println "*** You can run tests by one of the following commands:              *** "
        println "*** ./gradlew web                                                    *** "
        println "*** ./gradlew android                                                *** "
        println "*** ./gradlew ios                                                    *** "
        println "***                                                                  *** "
        println "************************************************************************ "
        println ""
    }
}